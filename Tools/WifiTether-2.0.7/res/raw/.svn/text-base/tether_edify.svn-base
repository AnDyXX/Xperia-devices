# This script controls actions to be taken when the tethering is started or stopped.
# @Version=52
# ------------------------------------------------------------------------------------
# It uses the "edify" language, which is also used for the android OTA update scripts. 
# See:
# http://android.git.kernel.org/?p=platform/bootable/recovery.git;a=tree;f=edify;h=04720f8aaa9a5e0079b79f8be7f11b7f74414162;hb=HEAD
# ------------------------------------------------------------------------------------

#
# Actions for starting tethering
#
action() == "start" && (
  #
  # Set "status"-Property
  #
  setprop("tether.status","running");

  is_substring("bahamas", getprop("ro.product.device")) &&
    run_program("/system/bin/ip route delete table gprs");

  #
  # Wifi mode, do some wifi things...
  #
  getcfg("tether.mode") == "wifi" && (
	#
	# Set "status"-Property
	#
    setprop("tether.mode","wifi");
  
    getcfg("device.type") == "nexusone" && (
      #
      # Nexus One.
      #
	  file_exists("/sdcard/android.tether/fw_bcm4329.bin") && (
      	module_loaded("bcm4329") || log(insmod("/system/lib/modules/bcm4329.ko", "firmware_path=/sdcard/android.tether/fw_bcm4329.bin"), "Loading bcm4329.ko module<br>(fw_bcm4329.bin from /sdcard/android.tether)");
      );      
      !file_exists("/sdcard/android.tether/fw_bcm4329.bin") && (
        file_exists("/etc/firmware/fw_bcm4329_ap.bin") && (
        	module_loaded("bcm4329") || log(insmod("/system/lib/modules/bcm4329.ko", "firmware_path=/etc/firmware/fw_bcm4329_ap.bin"), "Loading bcm4329.ko module<br>(fw_bcm4329_ap.bin from /etc/firmware)");
        );
        !file_exists("/etc/firmware/fw_bcm4329_ap.bin") && (
      		module_loaded("bcm4329") || log(insmod("/system/lib/modules/bcm4329.ko", ""), "Loading bcm4329.ko module");
      	);
      );
      sleep("3");
      #
      # Bring up wifi interface
      #
      log(run_program("/data/data/android.tether/bin/ifconfig " + getcfg("wifi.interface") + " " +
                  getcfg("ip.gateway") + " netmask " + getcfg("ip.netmask")) &&
                  run_program("/data/data/android.tether/bin/ifconfig " + getcfg("wifi.interface") + " up"),  "Activating WiFi interface");
      log(run_program("/data/data/android.tether/bin/iwconfig " + getcfg("wifi.interface")
                      + " mode ad-hoc"), "Setting ad-hoc mode");
      log(run_program("/data/data/android.tether/bin/iwconfig " + getcfg("wifi.interface")
                      + " essid " + getcfg("wifi.essid")), "Setting essid");
      log(run_program("/data/data/android.tether/bin/iwconfig " + getcfg("wifi.interface")
                      + " channel " + getcfg("wifi.channel")), "Setting channel");
      getcfg("wifi.txpower") != "disabled" && (
      	log(run_program("/data/data/android.tether/bin/iwconfig " + getcfg("wifi.interface")
                      + " txpower " + getcfg("wifi.txpower")), "Setting transmit power");     	
      );
    );
    
    is_substring("galaxy", getcfg("device.type")) && (
	  file_exists("/sdcard/android.tether/rtecdc.bin") && (
 	      module_loaded("bcm4325") || log(insmod("/system/libmodules/bcm4325.ko",
	                                             	"firmware_path=/sdcard/android.tether/rtecdc.bin nvram_path=/etc/nvram.txt"),
	                                      			"Loading bcm4325.ko module<br>(rtecdc.bin from /sdcard/android.tether)");
	  );     
      !file_exists("/sdcard/android.tether/rtecdc.bin") && (
		  !file_exists("/etc/rtecdc_adhoc.bin") && (
		      module_loaded("bcm4325") || log(insmod("/system/libmodules/bcm4325.ko", 
		      										"firmware_path=/etc/rtecdc.bin nvram_path=/etc/nvram.txt"), 
		      										"Loading bcm4325.ko module<br>(rtecdc.bin from /system/etc)");
		  );  
		  file_exists("/etc/rtecdc_adhoc.bin") && (
		      module_loaded("bcm4325") || log(insmod("/system/libmodules/bcm4325.ko", 
		      										"firmware_path=/etc/rtecdc_adhoc.bin nvram_path=/etc/nvram.txt"), 
		      										"Loading bcm4325.ko module<br>(rtecdc_adhoc.bin from /system/etc)");
		  );
	  );  
      sleep("3");
      #
      # Bring up wifi interface
      #
      log(run_program("/data/data/android.tether/bin/iwconfig " + getcfg("wifi.interface")
                      + " mode ad-hoc"), "Setting ad-hoc mode");
      log(run_program("/data/data/android.tether/bin/iwconfig " + getcfg("wifi.interface")
                      + " essid " + getcfg("wifi.essid")), "Setting essid");
      log(run_program("/data/data/android.tether/bin/iwconfig " + getcfg("wifi.interface")
                      + " channel " + getcfg("wifi.channel")), "Setting channel");
      run_program("/data/data/android.tether/bin/iwconfig " + getcfg("wifi.interface")
                      + " commit");
      log(run_program("/data/data/android.tether/bin/ifconfig " + getcfg("wifi.interface") + " " +
                  getcfg("ip.gateway") + " netmask " + getcfg("ip.netmask")) &&
                  run_program("/data/data/android.tether/bin/ifconfig " + getcfg("wifi.interface") + " up"),  "Activating WiFi interface");                  
    );
    
    getcfg("device.type") == "droid" && (
      #
      # Motorola droid
      #
      module_loaded("tiwlan_drv") || log(insmod("/system/lib/modules/tiwlan_drv.ko", ""), "Loading tiwlan_drv.ko module");
      run_program("/system/bin/wlan_loader -f /system/etc/wifi/fw_wlan1271.bin -e " +
                  "/proc/calibration -i /system/etc/wifi/tiwlan.ini");
      sleep("3");
      #
      # Bring up wifi interface
      #
      log(run_program("/data/data/android.tether/bin/ifconfig " + getcfg("wifi.interface") + " " +
                  getcfg("ip.gateway") + " netmask " + getcfg("ip.netmask")) &&
                  run_program("/data/data/android.tether/bin/ifconfig " + getcfg("wifi.interface") + " up"),  "Activating WiFi interface");
      log(run_program("/data/data/android.tether/bin/iwconfig " + getcfg("wifi.interface")
                      + " mode ad-hoc"), "Setting ad-hoc mode");
      log(run_program("/data/data/android.tether/bin/iwconfig " + getcfg("wifi.interface")
                      + " essid " + getcfg("wifi.essid")), "Setting essid");
      log(run_program("/data/data/android.tether/bin/iwconfig " + getcfg("wifi.interface")
                      + " channel " + getcfg("wifi.channel")), "Setting channel");
      run_program("/data/data/android.tether/bin/iwconfig " + getcfg("wifi.interface")
                      + " commit");
      run_program("/data/data/android.tether/bin/iwconfig "+getcfg("wifi.interface")+" mode ad-hoc essid "+getcfg("wifi.essid")+ " persist &");
    );
 
    getcfg("device.type") == "dream" && (
      #
      # HTC dream/magic/tattoo/eris
      #      
	  module_loaded("wlan") || log(insmod("/system/lib/modules/wlan.ko", ""), "Loading wlan.ko module");
      file_exists("/sdcard/android.tether/Fw1251r1c.bin") && (
 	      log(run_program("/system/bin/wlan_loader -f /sdcard/android.tether/Fw1251r1c.bin -e " +
	                  "/proc/calibration -i /data/data/android.tether/conf/tiwlan.ini"), "Configuring WiFi interface<br>(Fw1251r1c.bin from /sdcard/android.tether)");
      );
      !file_exists("/sdcard/android.tether/Fw1251r1c.bin") && (
	      file_exists("/system/etc/wifi/Fw1251r1c.bin") && (
		      log(run_program("/system/bin/wlan_loader -f /system/etc/wifi/Fw1251r1c.bin -e " +
		                  "/proc/calibration -i /data/data/android.tether/conf/tiwlan.ini"), "Configuring WiFi interface<br>(Fw1251r1c.bin from /system/etc/wifi)");
	      );
	      file_exists("/system/etc/firmware/Fw1251r1c.bin") && (
		      log(run_program("/system/bin/wlan_loader -f /system/etc/firmware/Fw1251r1c.bin -e " +
		                  "/proc/calibration -i /data/data/android.tether/conf/tiwlan.ini"), "Configuring WiFi interface<br>(Fw1251r1c.bin from /system/etc/firmware)");
	      );      
      );
      #
      # Bring up wifi interface
      #
      log(run_program("/data/data/android.tether/bin/ifconfig " + getcfg("wifi.interface") + " " +
                  getcfg("ip.gateway") + " netmask " + getcfg("ip.netmask")) &&
                  run_program("/data/data/android.tether/bin/ifconfig " + getcfg("wifi.interface") + " up"),  "Activating WiFi interface");
    );
	
	is_substring("hero", getcfg("device.type")) && (
	  #
	  # Removing route (gprs table) 
	  #
	  getcfg("device.type") == "hero1x" && 
	    run_program("/system/bin/ip route delete table gprs");      
      
      module_loaded("wlan") || log(insmod("/system/lib/modules/wlan.ko", ""), "Loading wlan.ko module");
      file_exists("/sdcard/android.tether/Fw1251r1c.bin") && (
 	      log(run_program("/system/bin/wlan_loader -f /sdcard/android.tether/Fw1251r1c.bin -e " +
	                  "/proc/calibration -i /data/data/android.tether/conf/tiwlan.ini"), "Configuring WiFi interface<br>(Fw1251r1c.bin from /sdcard/android.tether)");
      );     
      !file_exists("/sdcard/android.tether/Fw1251r1c.bin") && (
	      file_exists("/system/etc/wifi/Fw1251r1c.bin") && (
	      log(run_program("/system/bin/wlan_loader -f /system/etc/wifi/Fw1251r1c.bin -e " +
	                  "/proc/calibration -i /data/data/android.tether/conf/tiwlan.ini"), "Configuring WiFi interface<br>(Fw1251r1c.bin from /system/etc/wifi)");
	      );
	      file_exists("/system/etc/firmware/Fw1251r1c.bin") && (
	      log(run_program("/system/bin/wlan_loader -f /system/etc/firmware/Fw1251r1c.bin -e " +
	                  "/proc/calibration -i /data/data/android.tether/conf/tiwlan.ini"), "Configuring WiFi interface<br>(Fw1251r1c.bin from /system/etc/firmware)");
	      );      
      );
      #
      # Bring up wifi interface
      #
      log(run_program("/data/data/android.tether/bin/ifconfig " + getcfg("wifi.interface") + " " +
                  getcfg("ip.gateway") + " netmask " + getcfg("ip.netmask")) &&
                  run_program("/data/data/android.tether/bin/ifconfig " + getcfg("wifi.interface") + " up"),  "Activating WiFi interface");
    );

    getcfg("device.type") == "moment" && (
      #
      # Samsung Moment
      #
      file_exists("/sdcard/android.tether/rtecdc.bin") && (
      	module_loaded("dhd") || log(insmod("/lib/modules/dhd.ko", "firmware_path=/sdcard/android.tether/rtecdc.bin nvram_path=/etc/nvram.txt"), "Loading dhd.ko module");
      );      
      !file_exists("/sdcard/android.tether/rtecdc.bin") && (
        !file_exists("/etc/rtecdc_adhoc.bin") && (
      		module_loaded("dhd") || log(insmod("/lib/modules/dhd.ko", "firmware_path=/etc/rtecdc.bin nvram_path=/etc/nvram.txt"), "Loading dhd.ko module");
      	);
        file_exists("/etc/rtecdc_adhoc.bin") && (
      		module_loaded("dhd") || log(insmod("/lib/modules/dhd.ko", "firmware_path=/etc/rtecdc_adhoc.bin nvram_path=/etc/nvram.txt"), "Loading dhd.ko module");
      	);
      );      
      sleep("3");
      #
      # Bring up wifi interface
      #
      log(run_program("/data/data/android.tether/bin/ifconfig " + getcfg("wifi.interface") + " " +
                  getcfg("ip.gateway") + " netmask " + getcfg("ip.netmask")) &&
                  run_program("/data/data/android.tether/bin/ifconfig " + getcfg("wifi.interface") + " up"),  "Activating WiFi interface");
      log(run_program("/data/data/android.tether/bin/iwconfig " + getcfg("wifi.interface")
                      + " mode ad-hoc"), "Setting ad-hoc mode");
      log(run_program("/data/data/android.tether/bin/iwconfig " + getcfg("wifi.interface")
                      + " essid " + getcfg("wifi.essid")), "Setting essid");
      log(run_program("/data/data/android.tether/bin/iwconfig " + getcfg("wifi.interface")
                      + " channel " + getcfg("wifi.channel")), "Setting channel");
      run_program("/data/data/android.tether/bin/iwconfig " + getcfg("wifi.interface") + " commit");
    );

    getcfg("device.type") == "cliq" && (
      #
      # Motorola Cliq
      #
      module_loaded("dhd") || log(insmod("/system/lib/dhd.ko", "firmware_path=/etc/wifi/sdio-g-cdc-reclaim-wme.bin nvram_path=/etc/wifi/nvram.txt"), "Loading dhd.ko module");
      sleep("3");
      #
      # Bring up wifi interface
      #
      log(run_program("/data/data/android.tether/bin/ifconfig " + getcfg("wifi.interface") + " " +
                  getcfg("ip.gateway") + " netmask " + getcfg("ip.netmask")) &&
                  run_program("/data/data/android.tether/bin/ifconfig " + getcfg("wifi.interface") + " up"),  "Activating WiFi interface");
      log(run_program("/data/data/android.tether/bin/iwconfig " + getcfg("wifi.interface")
                      + " mode ad-hoc"), "Setting ad-hoc mode");
      log(run_program("/data/data/android.tether/bin/iwconfig " + getcfg("wifi.interface")
                      + " essid " + getcfg("wifi.essid")), "Setting essid");
      log(run_program("/data/data/android.tether/bin/iwconfig " + getcfg("wifi.interface")
                      + " channel " + getcfg("wifi.channel")), "Setting channel");
      run_program("/data/data/android.tether/bin/iwconfig " + getcfg("wifi.interface") + " commit");
    );

    getcfg("device.type") == "liquid" && (
      #
      # Acer Liquid
      #
      module_loaded("dhd") || log(insmod("/system/etc/wifi/dhd.ko", "firmware_path=/etc/wifi/BCM4325.bin nvram_path=/etc/wifi/nvram.txt"), "Loading dhd.ko module");
      sleep("3");
      #
      # Bring up wifi interface
      #
      log(run_program("/data/data/android.tether/bin/ifconfig " + getcfg("wifi.interface") + " " +
                  getcfg("ip.gateway") + " netmask " + getcfg("ip.netmask")) &&
                  run_program("/data/data/android.tether/bin/ifconfig " + getcfg("wifi.interface") + " up"),  "Activating WiFi interface");
      log(run_program("/data/data/android.tether/bin/iwconfig " + getcfg("wifi.interface")
                      + " mode ad-hoc"), "Setting ad-hoc mode");
      log(run_program("/data/data/android.tether/bin/iwconfig " + getcfg("wifi.interface")
                      + " essid " + getcfg("wifi.essid")), "Setting essid");
      log(run_program("/data/data/android.tether/bin/iwconfig " + getcfg("wifi.interface")
                      + " channel " + getcfg("wifi.channel")), "Setting channel");
      run_program("/data/data/android.tether/bin/iwconfig " + getcfg("wifi.interface") + " commit");
    );
  
    getcfg("device.type") == "legend" && (
      #
      # HTC Legend
      #
      module_loaded("sdio") || log(insmod("/system/lib/modules/sdio.ko",""), "Loading sdio.ko module");
      module_loaded("tiwlan_drv") || log(insmod("/system/lib/modules/tiwlan_drv.ko", ""), "Loading tiwlan_drv.ko module");
      run_program("/system/bin/tiwlan_loader -f /system/etc/wifi/Fw1273_CHIP.bin -e " +
                  "/proc/calibration -i /system/etc/wifi/tiwlan.ini");
      sleep("3");
      #
      # Bring up wifi interface
      #
      log(run_program("/data/data/android.tether/bin/ifconfig " +getcfg("wifi.interface") + " " +
                  getcfg("ip.gateway") + " netmask " + getcfg("ip.netmask")) &&
                  run_program("/data/data/android.tether/bin/ifconfig " + getcfg("wifi.interface") + " up"),  "Activating WiFi interface");
      log(run_program("/data/data/android.tether/bin/iwconfig " + getcfg("wifi.interface")
                      + " mode ad-hoc"), "Setting ad-hoc mode");
      log(run_program("/data/data/android.tether/bin/iwconfig " + getcfg("wifi.interface")
                      + " essid " + getcfg("wifi.essid")), "Setting essid");
      log(run_program("/data/data/android.tether/bin/iwconfig " + getcfg("wifi.interface")
                      + " channel " + getcfg("wifi.channel")), "Setting channel");
      
      run_program("/data/data/android.tether/bin/iwconfig " + getcfg("wifi.interface") + " commit");
      run_program("/data/data/android.tether/bin/iwconfig " + getcfg("wifi.interface") +" mode ad-hoc essid "+getcfg("wifi.essid")+ " persist &");
    );
    
    getcfg("device.type") == "unknown" && (
      #
      # Unknown device
      #    
      log("Unkown device-type. Your device is not supported!");
    );
    
    #
    # WEP-Encryption
    #
    getcfg("wifi.encryption") == "wep" && (
      getcfg("wifi.setup") == "iwconfig" && (      	
        log(run_program("/data/data/android.tether/bin/iwconfig " + getcfg("wifi.interface") + " key s:"+getcfg("wifi.wepkey")+"") &&
          run_program("/data/data/android.tether/bin/iwconfig " + getcfg("wifi.interface") + " key restricted"), "Activating encryption<br/>(iwconfig)");
        run_program("/data/data/android.tether/bin/iwconfig " + getcfg("wifi.interface") + " commit");
      );
      getcfg("wifi.setup") == "wpa_supplicant" && (      	
        sleep("2");
	log(run_program("cd /data/local/tmp; mkdir /data/local/tmp/wpa_supplicant; wpa_supplicant -B -D" +
	  getcfg("wifi.driver") +
	  " -i" + getcfg("wifi.interface") +
	  " -c/data/data/android.tether/conf/wpa_supplicant.conf"), "Activating encryption<br/>(wpa_supplicant)");
      );
    );
  );

  getcfg("tether.mode") == "bt" && (
    #
    # Set "mode"-Property
    #
    setprop("tether.mode","bt");

    #
    # Bluetooth - start pand
    #
    run_program("/data/data/android.tether/bin/pand --listen --role NAP " +
                "--devup /data/data/android.tether/bin/blue-up.sh " +
                "--devdown /data/data/android.tether/bin/blue-down.sh " +
                "--pidfile /data/data/android.tether/var/pand.pid");
  );

  #
  # Remove old rules
  #
  run_program("/data/data/android.tether/bin/iptables -N wireless-tether");
  run_program("/data/data/android.tether/bin/iptables -F wireless-tether");  
  run_program("/data/data/android.tether/bin/iptables -t nat -F PREROUTING");
  run_program("/data/data/android.tether/bin/iptables -t nat -F POSTROUTING");
  run_program("/data/data/android.tether/bin/iptables -t nat -F");

  #
  # Bring up NAT rules
  #
  log(
    run_program("/data/data/android.tether/bin/iptables -A wireless-tether -m state --state ESTABLISHED,RELATED -j ACCEPT") &&
    run_program("/data/data/android.tether/bin/iptables -A wireless-tether -s " + getcfg("ip.network") + "/24 -j ACCEPT") &&
    run_program("/data/data/android.tether/bin/iptables -A wireless-tether -j DROP") &&
    run_program("/data/data/android.tether/bin/iptables -A FORWARD -j wireless-tether") &&
    run_program("/data/data/android.tether/bin/iptables -t nat -I POSTROUTING -s " +
               getcfg("ip.network") + "/24 -j MASQUERADE"),
    "Enabling NAT rules");

  #
  # IP forwarding
  #
  log(file_write("/proc/sys/net/ipv4/ip_forward", "1"), "Enabling IP forwarding");

  #
  # dnsmasq for wifi tether (bluetooth has pand start it)
  #
  getcfg("tether.mode") == "wifi" &&
      run_program("/data/data/android.tether/bin/dnsmasq -i " + getcfg("wifi.interface") +" "+
                  "--resolv-file=/data/data/android.tether/conf/resolv.conf " +
                  "--conf-file=/data/data/android.tether/conf/dnsmasq.conf");

  #
  # Access control
  #
  file_exists("/data/data/android.tether/conf/whitelist_mac.conf") &&
    log(
      run_program("/data/data/android.tether/bin/iptables -t nat -I PREROUTING -s " + getcfg("ip.network") + "/24 -j DROP") &&
      whitelist_macs("/data/data/android.tether/conf/whitelist_mac.conf"),
    "Enabling access control."
    );
  log("Tethering now running");
);

#
# Actions when stopping tether
#
action() == "stop" && (
  #
  # Set "status"-Property
  #
  setprop("tether.status","stopped");

  #
  # Bluetooth, kill pand and and dnsmasq processes
  #
  getcfg("tether.mode") == "bt" && (
    run_program("/data/data/android.tether/bin/pand -K");
    sleep("1");
    kill_process("pand");
    file_unlink("/data/data/android.tether/var/pand.pid");
    kill_process("dnsmasq");
  );
  
  #
  # Wifi mode, bring interface down, kill dnsmasq/wpa_supplicant, remove module.
  #
  getcfg("tether.mode") == "wifi" && (
    run_program("/data/data/android.tether/bin/ifconfig " +  getcfg("wifi.interface") + " down");
    kill_process("wpa_supplicant");
    kill_process("dnsmasq");
	kill_process("iwconfig");
    module_loaded("bcm4329") && rmmod("bcm4329");
    module_loaded("bcm4325") && rmmod("bcm4325");
    module_loaded("wlan") && rmmod("wlan");
    module_loaded("tiwlan_drv") && rmmod("tiwlan_drv");
    module_loaded("sdio") && rmmod("sdio");
    module_loaded("dhd") && rmmod("dhd");
  );

  #
  # Remove old dnsmasq.leases and pid-file
  #
  file_exists("/data/data/android.tether/var/dnsmasq.leases") && (
  	file_unlink("/data/data/android.tether/var/dnsmasq.leases");
  );
  file_exists("/data/data/android.tether/var/dnsmasq.pid") && (
    file_unlink("/data/data/android.tether/var/dnsmasq.pid");
  );
  file_exists("/data/data/android.tether/var/setssid.pid") && (
    file_unlink("/data/data/android.tether/var/setssid.pid");
  );

  #
  # Disable forwarding and remove NAT rules.
  #
  log(file_write("/proc/sys/net/ipv4/ip_forward", "0"), "Disabling forwarding");
  log(
    run_program("/data/data/android.tether/bin/iptables -D FORWARD -j wireless-tether") &&
    run_program("/data/data/android.tether/bin/iptables -F wireless-tether") &&  
    run_program("/data/data/android.tether/bin/iptables -X wireless-tether") && 
    run_program("/data/data/android.tether/bin/iptables -t nat -F PREROUTING") &&
    run_program("/data/data/android.tether/bin/iptables -t nat -F POSTROUTING") &&
    run_program("/data/data/android.tether/bin/iptables -t nat -F"),
  "Disabling NAT rules");

  log("Tethering now stopped");
  
);

#
# Actions when restarting access control
#
action() == "restartsecwifi" && (
  #
  # Remove old rules
  #
  log (
    run_program("/data/data/android.tether/bin/iptables -t nat -F PREROUTING") &&
    run_program("/data/data/android.tether/bin/iptables -t nat -F POSTROUTING"),
  "Disabling NAT rules");

  #
  # Bring up NAT rules
  #
  log(
    run_program("/data/data/android.tether/bin/iptables -t nat -I POSTROUTING -s " +
               getcfg("ip.network") + "/24 -j MASQUERADE"),
    "Enabling NAT rules");

  #
  # Access control
  #
  file_exists("/data/data/android.tether/conf/whitelist_mac.conf") &&
    log(
      run_program("/data/data/android.tether/bin/iptables -t nat -I PREROUTING -s " + getcfg("ip.network") + "/24 -j DROP") &&
      whitelist_macs("/data/data/android.tether/conf/whitelist_mac.conf"),
    "Enabling access control."
    );
);
